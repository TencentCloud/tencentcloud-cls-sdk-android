plugins {
    id 'org.jreleaser' version '1.19.0'
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

version = '2.0.0'
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.tencentcloudapi.cls'
                artifactId = 'cls-network-diagnosis-reporter-android'
                version = project.version
                from components.release

                pom {
                    name = artifactId
                    description = "tencent cloud cls log sdk"
                    url = 'https://github.com/TencentCloud/tencentcloud-cls-sdk-android'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'farmerx'
                            name = 'farmerx'
                            email = 'farmerx@163.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/TencentCloud/tencentcloud-cls-sdk-android.git'
                        developerConnection = 'scm:git:ssh://git@github.com/TencentCloud/tencentcloud-cls-sdk-android.git'
                        url = 'https://github.com/TencentCloud/tencentcloud-cls-sdk-android/'
                    }
                }
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    verifyPom = false
                    namespace = 'com.tencentcloudapi.cls'
                    snapshotSupported = true
                }
            }
        }
    }
    gitRootSearch = true
}

android {
    compileSdk 33

    namespace "com.tencentcloudapi.cls.plugin.network_diagnosis"

    defaultConfig {
        minSdk 19
        targetSdk 33
        versionCode 1
        versionName "2.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation project(path: ':tencentcloud_cls_producer')
    implementation 'com.google.guava:guava:27.0.1-android'
    implementation 'com.squareup.okhttp3:okhttp:3.14.2'
    implementation 'com.squareup.okio:okio:1.17.4'
    testImplementation 'junit:junit:4.+'
}
